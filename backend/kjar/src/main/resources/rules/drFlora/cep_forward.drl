import com.ftn.sbnz.model.FinalnaDijagnoza;
import com.ftn.sbnz.model.Preparat;
import com.ftn.sbnz.model.Bolest;
import com.ftn.sbnz.model.enums.FazaBiljke;
import com.ftn.sbnz.model.enums.PotkategorijaPreparata;
import com.ftn.sbnz.model.Preporuka;
import java.time.LocalDate;
import com.ftn.sbnz.model.Biljka;
import com.ftn.sbnz.model.enums.KategorijaPreparata;
import com.ftn.sbnz.event.NeizlecenaBolestVoca

declare DuplaDozaSlabogPreparataAlarm
    @role(event)
    biljka: Biljka
    bolest: Bolest
end

declare PromenaSlabogUJakPreparatAlarm
    @role(event)
    biljka: Biljka
    bolest: Bolest
end

declare NapomenaZaInsekticidAlarm
    @role(event)
    biljka: Biljka
    bolest: Bolest
    preparat: Preparat
end

rule "Dijagnoza iste bolesti 2 ili vise puta kod iste biljke u istoj fazi u mesec dana -> aktivirati alarm NeizlecenaBolestVoca"
    no-loop
    when 
        $dijagnoza: FinalnaDijagnoza($id: id, 
                                    $bolest: bolest, 
                                    $preparat: preporuceniPreparat, 
                                    $biljka: biljka, 
                                    $fazaBiljke: fazaBiljke,
                                    datumPreporuke.isAfter(LocalDate.now().minusMonths(1)))
        Number(intValue >= 1) from accumulate(
            $d : FinalnaDijagnoza(this != $dijagnoza,
                                          bolest.id == $bolest.id,
                                          biljka.id == $biljka.id,
                                          fazaBiljke == $fazaBiljke,
                                          datumPreporuke.isAfter(LocalDate.now().minusMonths(1))
                                         ), 
                                          count($d)
        )
        not (NeizlecenaBolestVoca(biljka == $biljka, bolest == $bolest, fazaJavljanja == $fazaBiljke))
    then
        System.out.println("Neizlecena bolest voca za " + $biljka.getId() + ", bolest " + $bolest.getNaziv());
        insert(new NeizlecenaBolestVoca($biljka, $bolest, $preparat, $fazaBiljke));
end

rule "NeizlecenaBolestVoca i preporuceni slabi preparat za bolest za koju ne postoji jak preparat -> preporuciti duplu dozu preparata" 
    no-loop
    when 
        $alarm: NeizlecenaBolestVoca($biljka: biljka, 
                                    $bolest: bolest,  
                                    bolest.jakiPreparati.size() == 0,
                                    $fazaBiljke : fazaJavljanja, 
                                    $preparat: preparat, 
                                    preparat.potkategorija == PotkategorijaPreparata.SLAB
                                    ) //dodati da se ovaj alarm sad desio, da se ne aktivira na neki raniji
        
        $dijagnoza : FinalnaDijagnoza(bolest.id == $bolest.id,
                                    biljka.id == $biljka.id,
                                    preporuceniPreparat.id == $preparat.id,
                                    $preporuka: preporuka)
        
        // $preporuka: Preporuka(bolest.id == $bolest.id,
        //                       nazivPreparata == $preparat.naziv,
        //                       biljka.id == $biljka.id)
        
        not(DuplaDozaSlabogPreparataAlarm(biljka == $biljka, bolest == $bolest))
        
    then
        System.out.println("Dupla doza " + $biljka.getId() + ", bolest " + $bolest.getNaziv());
        modify($preporuka) {setPreporucenaDoza($preporuka.getPreporucenaDoza() * 2)}
        insert(new DuplaDozaSlabogPreparataAlarm($biljka, $bolest));
end

rule "NeizlecenaBolestVoca i preporuceni slabi preparat za bolest za koju postoji jak preparat -> preporuciti jak preparat" 
    no-loop
    when 
        $alarm: NeizlecenaBolestVoca($biljka: biljka, 
                                    $bolest: bolest,  
                                    bolest.jakiPreparati.size() > 0,
                                    $fazaBiljke : fazaJavljanja, 
                                    $preparat: preparat, 
                                    preparat.potkategorija == PotkategorijaPreparata.SLAB
                                    ) //dodati da se ovaj alarm sad desio, da se ne aktivira na neki raniji
        
        $dijagnoza : FinalnaDijagnoza(bolest.id == $bolest.id,
                                    biljka.id == $biljka.id,
                                    preporuceniPreparat.id == $preparat.id,
                                    $preporuka: preporuka)
        
        // $preporuka: Preporuka(bolest.id == $bolest.id,
        //                       nazivPreparata == $preparat.naziv,
        //                       biljka.id == $biljka.id)

        not(PromenaSlabogUJakPreparatAlarm(biljka == $biljka, bolest == $bolest))
        
    then
        modify($dijagnoza) {setPreparat($bolest.getTopJakPreparat())}
        modify($preporuka) {setPreporucenaDoza($bolest.getTopJakPreparat().getKoncentracija()), 
                                    setNazivPreparata($bolest.getTopJakPreparat().getNaziv()),
                                    setOpisPreparata($bolest.getTopJakPreparat().getPotkategorijaPreparata() + " " + $bolest.getTopJakPreparat().getKategorijaPreparata()) }
        modify($alarm) {setPreparat($bolest.getTopJakPreparat())}
        insert(new PromenaSlabogUJakPreparatAlarm($biljka, $bolest));
        System.out.println("Neizlecena bolest alarm jak preparat " + $alarm.getPreparat().getNaziv());
end

rule "NeizlecenaBolestVoca i preporuceni jak preparat insekticid -> napomena za upotrebu u ishrani" 
    no-loop
    when 
        $alarm: NeizlecenaBolestVoca($biljka: biljka, 
                                    $bolest: bolest,  
                                    bolest.jakiPreparati.size() > 0,
                                    $fazaBiljke : fazaJavljanja,
                                    $preparat: preparat,       
                                    preparat.potkategorija == PotkategorijaPreparata.JAK,                         
                                    preparat.primarnaKategorija == KategorijaPreparata.INSEKTICID                                  
                                    ) //dodati da se ovaj alarm sad desio, da se ne aktivira na neki raniji
                
        $preporuka: Preporuka(bolest.id == $bolest.id,
                              nazivPreparata == $preparat.naziv,
                              biljka.id == $biljka.id)
        
        not(NapomenaZaInsekticidAlarm(biljka == $biljka, bolest == $bolest, preparat == $preparat))
        
    then
        insert(new NapomenaZaInsekticidAlarm($biljka, $bolest, $preparat));
        modify($preporuka) {setPoruka("Biljku ne upotrebljavati u ishrani do 2 nedelje nakon prskanja")}
end




